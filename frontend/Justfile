region := env("AWS_REGION", "eu-west-1")
meta_path := justfile_directory() + "/meta.json"

export AWS_REGION := region
export AWS_PAGER := ""

meta:
    [ -f "{{meta_path}}" ] || echo "{}" > "{{meta_path}}"

build domain client_id:
    #!/bin/bash
    export VITE_COGNITO_DOMAIN_URL="{{domain}}"
    export VITE_COGNITO_CLIENT_ID="{{client_id}}"
    timestamp=$(date +%s)
    export VITE_BUILD_VERSION="$timestamp"
    export VITE_BUILD_TIMESTAMP="$timestamp"
    npm run build

[working-directory: "mock"]
build-mock:
    go build .

[working-directory: "mock"]
run-mock: build-mock
    ./mock

preview:
    VITE_IDP_URL="http://localhost:9090" VITE_COGNITO_DOMAIN_URL="http://localhost:9090" VITE_COGNITO_CLIENT_ID="1234" npm run build
    npm run preview

[parallel]
preview-with-mock: run-mock preview

[working-directory: "infra"]
tf-fmt:
    tofu fmt -recursive .

[working-directory: "infra"]
tf-apply: meta
    #!/bin/bash -e
    tofu init
    tofu apply -auto-approve -var-file="dev.tfvars"
    bucket=$(tofu output -raw 'bucket_name')
    jq --arg bucket "$bucket" '.bucket = $bucket' "{{meta_path}}" | sponge "{{meta_path}}"

[working-directory: "infra"]
tf-destroy:
    tofu destroy -auto-approve -var-file="dev.tfvars"

deploy: meta
    #!/bin/bash -e
    bucket=$(jq -r '.bucket' "{{meta_path}}")
    aws s3 cp --recursive "{{justfile_directory()}}/dist/" "s3://${bucket}/"

prettier:
    npx prettier --write "src/**/*.{ts,tsx,js,jsx}"